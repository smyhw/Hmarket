plugins {
    id 'java-library'
    id 'maven-publish'
    id("xyz.jpenilla.run-paper") version "1.0.6"
}

// Plugin config
var minecraftVersionStr = "1.20.4"
ext.pluginName = "Hmarket"
ext.pluginname = ext.pluginName.toLowerCase()
ext.majorVersion = 0
ext.minorVersion = 9
ext.minecraftVersion = minecraftVersionStr

// Supplied by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x" : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null : "$System.env.JAVADOCS_DIR"

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
//archivesBaseName = "${pluginName}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber".toString()
runServer {
    minecraftVersion(minecraftVersionStr)
}
repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { name 'NyaaCat'; url 'https://ci.nyaacat.com/maven/' }
    maven { url 'https://jitpack.io' }
}
java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    withJavadocJar()
    withSourcesJar()
}
dependencies {
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly("io.papermc.paper:paper-api:$minecraftVersion-R0.1-SNAPSHOT")
    // other nyaa plugins
    compileOnly('cat.nyaa:nyaacore:9.3.4-1.20.4')
//    compileOnly('cat.nyaa:ecore:0.3.3')
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0'
    implementation "net.kyori:adventure-api:4.14.0"
    implementation("net.kyori:adventure-platform-bukkit:4.3.0")
}

compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
    options.encoding = 'UTF-8'
}

processResources { // modify version string
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            group project.group
            artifactId pluginname
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url mavenDirectory
        }
    }
}
javadoc {
    if (project.jdDirectory != null) destinationDir = file("${jdDirectory}/${pluginname}-${version}")
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://guava.dev/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')
        options.addBooleanOption('html5', true)
        windowTitle = "${pluginName} Javadoc"
    }
}